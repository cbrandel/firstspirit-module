/*
 * Sample build.gradle for FirstSpirit .fsm packaging
 * https://docs.e-spirit.com/odfs/plug-developmen/implementation/module-architec/index.html
 * some ideas from here  https://stackoverflow.com/questions/25398703/gradle-analogue-for-maven-assembly-plugin
 */

plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
	id 'eclipse'
	id 'distribution'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}
dependencies {
	// api, implementation deps are packaged in the fsm and added to the <resources>
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
	// compileOnly deps are not packaged and dnot added to the <resources>
    compileOnly 'commons-io:commons-io:2.11.0'
    
    // test stuff is also not packaged and dnot added to the <resources>
    // Use JUnit test framework.
    testImplementation 'junit:junit:4.13.2'
    
}

ext {
	// some properties for use in the module.xml
	// list of jars that are packaged
	jarlist = configurations.runtimeClasspath.collect{"${it.name}"}
	// ready-made list of <resources> entries for the fsm module.xml
	fsmResourcesList = configurations.runtimeClasspath.collect{"<resource scope=\"server\">lib/${it.name}</resource>"}
	// displayname Suffix
	fsmIsolationDisplayName = (fsmIsolationMode == "il" ? "(I, L)" : (fsmIsolationMode== "i"? "(I)" : "(L)"))
}
// Do packaging for FirstSpirit Modules as .fsm
import org.apache.tools.ant.filters.ReplaceTokens
distributions {
  main {
   	distributionBaseName = project.name
   	// use leading slash for path to omit addition subdirectory in the zip
    contents {
      into('/lib/') {  // Copy the following jars to the /lib/ directory in the distribution archive
        from jar
        from configurations.runtimeClasspath
        dirMode = 0755
        fileMode = 0644
      }
      into('/META-INF/') {
	    from('src/main/fsm-resources') {
	    	expand(project: project)
	    }
	    dirMode = 0755
	    fileMode = 0644
	  }
    }
  }
}

distZip {
	archiveExtension = 'fsm'
}


// https://github.com/gradle/gradle/issues/3839#issuecomment-512805239
eclipse {
    classpath {
        defaultOutputDir = file('build') // this should be enough
        file {
            whenMerged { // but we still need to adjust the outputs for src paths
                cp -> cp.getEntries().forEach{
                    cpEntry -> if(cpEntry.kind=='src') {
                        cpEntry.output = cpEntry.output.replace('bin/', 'build/')
                    }
                }
            }
        }
    }
}
